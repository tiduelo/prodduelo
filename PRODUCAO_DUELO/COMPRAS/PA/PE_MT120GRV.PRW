#include "rwmake.ch"
#include "protheus.ch"
/*
????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
???????????????????????????????????????????????????????????????????????????
???Programa ? MT120GRV ? RAFAEL ALMEIDA - SIGACORP ? Data ? 09/08/2019   ???
????????????????????????????????????????????????????????????????????????J??
???Descricao ? Ponto de Entrada p/ excluir pedido de compra              ???
????????????????????????????????????????????????????????????????????????J??
???Sintaxe ? Chamada padrao para programas em RDMake. ???
???????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????
????????????????????????????????????????????????????????????????????????????*/

User Function  MT120GRV ()
Local lRet := .T.
Local _lExclui := ParamIxb[4]
Local _cNumPc   := PadR("PC"+ParamIxb[1], 9 )
Local _cCondPag := SC7->C7_COND
Local _cCodForn := SC7->C7_FORNECE
Local _cLojForn := SC7->C7_LOJA
Local _cCondPA  := ""
Private lMsErroAuto := .F.

_cCondPA := POSICIONE("SE4", 1, xFilial("SE4") + _cCondPag, "E4_CTRADT")

If _cCondPA == "1" .And. _lExclui
	DbSelectArea("SE2")
	DbSetOrder(1)
	DbGotop()
	If DbSeek(xFilial("SE2") +  "ANT" +   _cNumPc + " " + "PA " + _cCodForn + _cLojForn  )
		If  !Empty(SE2->E2_FLAGPA)
			lRet := .F.
			_cMsg := "Seu pedido n?o poder? ser exclu?do! Pois houve movimenta??o financeira."+CRLF
			_cMsg += "Por favor "+CRLF
			_cMsg += "Entrar em contato com Departamento Financeiro para maiores informa??es."
			MsgBox(_cMsg,"Aten??o","ALERT")
		Else
			
			aArray := { { "E2_PREFIXO" , SE2->E2_PREFIXO , NIL },;
			{ "E2_NUM" 	   , SE2->E2_NUM     , NIL },;
			{ "E2_PARCELA" , SE2->E2_PARCELA , NIL },;
			{ "E2_TIPO"    , SE2->E2_TIPO    , NIL },;
			{ "E2_FORNECE" , SE2->E2_FORNECE , NIL },;
			{ "E2_LOJA"    , SE2->E2_LOJA    , NIL } }
			
			DbSelectArea("FIE")
			DbSetOrder(1)
			DbGotop()
			If DbSeek(xFilial("FIE") +  "P" + ParamIxb[1] )
				RecLock("FIE",.F.)
				FIE->(DbDelete())
				FIE->(MsUnLock())
			EndIf
			
			MsExecAuto( { |x,y,z| FINA050(x,y,z)}, aArray,, 5)  // 3 - Inclusao, 4 - Altera??o, 5 - Exclus?o
			
			If lMsErroAuto
				MostraErro()
				lRet := .F.
			Else
				Alert("Exclus?o do T?tulo de Adiantamento com sucesso!")
				lRet := .T.
			Endif
		EndIf
	EndIf
EndIf
Return(lRet)
